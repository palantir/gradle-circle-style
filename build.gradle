buildscript {
  repositories {
    jcenter()
    maven { url  'https://palantir.bintray.com/releases' }
  }
  dependencies {
    classpath 'com.palantir.baseline:gradle-baseline-java:0.38.1'
  }
}

plugins {
  id 'com.gradle.plugin-publish' version '0.10.0'
  id 'com.palantir.circle.style' version '1.2.0-rc3'
  id 'org.inferred.processors' version '2.0.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'com.palantir.baseline'

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://palantir.bintray.com/releases' }
}

sourceCompatibility = 1.7

dependencies {
  compile gradleApi()

  annotationProcessor 'org.inferred:freebuilder'
  compileOnly 'org.inferred:freebuilder'

  testCompile gradleTestKit()
  testCompile 'com.github.stefanbirkner:system-rules'
  testCompile 'com.google.guava:guava'
  testCompile 'junit:junit'
  testCompile 'org.assertj:assertj-core'
  testCompile 'org.mockito:mockito-core'
  testCompile 'me.nallar.whocalled:WhoCalled'
}

// Extra validation steps
tasks.check.dependsOn tasks.javadoc

group = 'com.palantir'
if (System.env.CIRCLE_TAG) {
  version = System.env.CIRCLE_TAG.replaceAll('^v', '')
}

tasks.publish.dependsOn 'publishPlugins'

pluginBundle {
  website = 'https://github.com/palantir/gradle-circle-style'
  vcsUrl = 'https://github.com/palantir/gradle-circle-style'

  plugins {
    circleStylePlugin {
      displayName = 'Circle style plugin'
      id = 'com.palantir.circle.style'
      description = 'Summarize Gradle build failures in CircleCI'
      tags = ['java', 'circle', 'circleci', 'checkstyle', 'findbugs']
    }
  }
}

String envOrThrow(String key) {
  def value = System.getenv(key)
  if (!value) {
    throw new RuntimeException("\$$key not set")
  }
  return value
}

// Read publication secrets from environment
task setupPublishProperties {
  doLast {
    System.properties.setProperty('gradle.publish.key', envOrThrow('gradlePublishKey'))
    System.properties.setProperty('gradle.publish.secret', envOrThrow('gradlePublishSecret'))
  }
}

tasks.publishPlugins.dependsOn 'setupPublishProperties'

publishPlugins.onlyIf {
  System.env.CIRCLE_TAG
}
