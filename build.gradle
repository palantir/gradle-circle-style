plugins {
  id 'java'
  id 'eclipse'
  id 'idea'
  id 'com.gradle.plugin-publish' version '0.9.7'
  id 'com.palantir.circle.style' version '1.2.0-rc2'
  id 'org.inferred.processors' version '1.2.15'
}

repositories {
    jcenter()
    mavenCentral()
}

sourceCompatibility = 1.7

dependencies {
  compile gradleApi()

  processor 'org.inferred:freebuilder:1.14.6'

  testCompile gradleTestKit()
  testCompile 'com.github.stefanbirkner:system-rules:1.16.0'
  testCompile 'com.google.guava:guava:23.0-android'
  testCompile 'junit:junit:4.12'
  testCompile 'org.assertj:assertj-core:2.9.0'
  testCompile 'org.mockito:mockito-core:2.15.0'
  testCompile 'me.nallar.whocalled:WhoCalled:1.1'
}

// Extra validation steps
tasks.check.dependsOn tasks.javadoc

group = 'com.palantir'
if (System.env.CIRCLE_TAG) {
  version = System.env.CIRCLE_TAG.replaceAll('^v', '')
}

pluginBundle {
  website = 'https://github.com/palantir/gradle-circle-style'
  vcsUrl = 'https://github.com/palantir/gradle-circle-style'

  plugins {
    circleStylePlugin {
      displayName = 'Circle style plugin'
      id = 'com.palantir.circle.style'
      description = 'Summarize Gradle build failures in CircleCI'
      tags = ['java', 'circle', 'circleci', 'checkstyle', 'findbugs']
    }
  }
}

String envOrThrow(String key) {
  def value = System.getenv(key)
  if (!value) {
    throw new RuntimeException("\$$key not set")
  }
  return value
}

// Read publication secrets from environment
task setupPublishProperties {
  tasks.publishPlugins.dependsOn it
  doLast {
    System.properties.setProperty('gradle.publish.key', envOrThrow('gradlePublishKey'))
    System.properties.setProperty('gradle.publish.secret', envOrThrow('gradlePublishSecret'))
  }
}

// Download dependencies to Gradle's cache
task resolveDependencies {
  doLast {
    project.rootProject.allprojects.each { subProject ->
      subProject.buildscript.configurations.each { if (it.canBeResolved) it.resolve() }
      subProject.configurations.each { if (it.canBeResolved) it.resolve() }
    }
  }
}
